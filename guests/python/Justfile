PYTHON_VERSION_MAJOR := "3"
PYTHON_VERSION_MINOR := "14"
PYTHON_VERSION_MICRO := "0"

WASI_SDK_VERSION_MAJOR := "24"
WASI_SDK_VERSION_MINOR := "0"

SHA256_PYTHON_SDK := "54aa3e33ebb45e03b5c13b86ce8742b45bb94e394925622fa66c4700f6782979"
SHA256_PYTHON_SDK_BUILD := "98e7cd352e512d1d2af6f999f4391df959eba1b213ef778646353620d697638a"
SHA256_WASI_SDK_SYSROOT := "35172f7d2799485b15a46b1d87f50a585d915ec662080f005d99153a50888f08"

PYTHON_VERSION_FULL := PYTHON_VERSION_MAJOR + "." + PYTHON_VERSION_MINOR + "." + PYTHON_VERSION_MICRO

# Configurable URLs for Python SDK downloads (with defaults)
PYTHON_SDK_BASE_URL := env_var_or_default("PYTHON_SDK_BASE_URL", "https://github.com/brettcannon/cpython-wasi-build/releases/download")
PYTHON_SDK_URL := env_var_or_default("PYTHON_SDK_URL", PYTHON_SDK_BASE_URL + "/v" + PYTHON_VERSION_FULL + "/python-" + PYTHON_VERSION_FULL + "-wasi_sdk-" + WASI_SDK_VERSION_MAJOR + ".zip")
BUILD_PYTHON_SDK_URL := env_var_or_default("BUILD_PYTHON_SDK_URL", PYTHON_SDK_BASE_URL + "/v" + PYTHON_VERSION_FULL + "/_build-python-" + PYTHON_VERSION_FULL + "-wasi_sdk-" + WASI_SDK_VERSION_MAJOR + ".zip")

DOWNLOADS_DIR := source_directory() / "downloads"

export PYO3_CROSS_PYTHON_VERSION := PYTHON_VERSION_MAJOR + "." + PYTHON_VERSION_MINOR
export PYO3_CROSS_LIB_DIR := DOWNLOADS_DIR / "python-sdk"

PYTHON_TMP_VENV := DOWNLOADS_DIR / "venv"
PYTHON_SITE_PACKAGES := PYO3_CROSS_LIB_DIR / "lib" / "python" + PYO3_CROSS_PYTHON_VERSION / "site-packages"

# Not used in the script but the linker must know where to find the WASI sysroot libs.
export WASI_SDK_LINK_PATH := DOWNLOADS_DIR / "wasi-sysroot" / "lib" / "wasm32-wasip2"

# download pre-built Python WASM SDK
[private]
download-python-sdk:
    #!/usr/bin/env bash
    set -euo pipefail

    echo ::group::guests::python::download-python-sdk
    set -x

    mkdir -p downloads
    pushd downloads >/dev/null

    # skip if already downloaded
    if [ -d python-sdk ]; then
        echo "python sdk already present"
        set +x
        echo ::endgroup::
        exit 0
    fi

    curl \
        --fail \
        --show-error \
        --silent \
        --proto '=https' \
        --tlsv1.2 \
        --location \
        --output "python-sdk.zip" \
        "{{PYTHON_SDK_URL}}"

    curl \
        --fail \
        --show-error \
        --silent \
        --proto '=https' \
        --tlsv1.2 \
        --location \
        --output "build-python-sdk.zip" \
        "{{BUILD_PYTHON_SDK_URL}}"

    echo "{{SHA256_PYTHON_SDK}} python-sdk.zip" | sha256sum -c
    echo "{{SHA256_PYTHON_SDK_BUILD}} build-python-sdk.zip" | sha256sum -c

    unzip -d python-sdk python-sdk.zip
    unzip -d build-python-sdk build-python-sdk.zip

    mv build-python-sdk/libpython{{PYTHON_VERSION_MAJOR}}.{{PYTHON_VERSION_MINOR}}.a python-sdk/
    mv build-python-sdk/Modules/_decimal/libmpdec/libmpdec.a python-sdk/
    mv build-python-sdk/Modules/_hacl/libHacl_HMAC.a python-sdk/
    mv build-python-sdk/Modules/_hacl/libHacl_Hash_BLAKE2.a python-sdk/
    mv build-python-sdk/Modules/_hacl/libHacl_Hash_MD5.a python-sdk/
    mv build-python-sdk/Modules/_hacl/libHacl_Hash_SHA1.a python-sdk/
    mv build-python-sdk/Modules/_hacl/libHacl_Hash_SHA2.a python-sdk/
    mv build-python-sdk/Modules/_hacl/libHacl_Hash_SHA3.a python-sdk/
    mv build-python-sdk/Modules/expat/libexpat.a python-sdk/

    set +x
    echo ::endgroup::

[private]
python-site-packages: download-python-sdk
    #!/usr/bin/env bash
    set -euo pipefail

    echo ::group::guests::python::python-site-packages
    set -x

    # skip if already downloaded
    if [ -d "{{PYTHON_TMP_VENV}}" ]; then
        echo "site-packages already set up"
        set +x
        echo ::endgroup::
        exit 0
    fi

    python -m venv "{{PYTHON_TMP_VENV}}"

    # Notes:
    # - "only binary" means "only wheels, no sdist", i.e. we don't need to run any Python lib build code
    # - "implementation=py" ensures that we use the most neutral wheel possible, i.e. not actual binary code
    "{{PYTHON_TMP_VENV}}"/bin/pip install \
        --disable-pip-version-check \
        --no-deps \
        --ignore-installed \
        --implementation=py \
        --isolated \
        --only-binary=:all: \
        --python-version={{PYTHON_VERSION_FULL}} \
        --requirement=requirements.txt \
        --target="{{PYTHON_SITE_PACKAGES}}"

    set +x
    echo ::endgroup::

[private]
build-lib profile: download-python-sdk python-site-packages
    @echo ::group::guests::python::build-lib {{profile}}
    cargo build --target=wasm32-wasip2 --profile={{replace(profile, "debug", "dev")}}
    @echo ::endgroup::

# top-level entry point
[private]
do profile: (build-lib profile)

# create dev/debug build
build-debug: (do "debug")

# create release build
build-release: (do "release")

# checks build
check-build: build-debug

# clean build artifacts
clean:
    @echo ::group::guests::python::clean
    rm -rf {{DOWNLOADS_DIR}}
    @echo ::endgroup::
